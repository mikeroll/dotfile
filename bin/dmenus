#!/usr/bin/zsh

typeset -a defaults=(
     -fn "Iosevka Nerd Font:size=11" 
     -i 
     -h '32' -y '-2' -x '-8'
     -class Dmenu
     -nb '#f3f3f3'
     -sb '#545c6a'
     -nf '#373b42'
     -sf '#f3f3f3'
)

function launch() {
  local opts=($defaults -w 860 -p "  " -q -dim 0.42)

  typeset -A executables=(${(kv)commands} ${(kv)functions} ${(kv)aliases})
  local choice="$(echo ${(F)${(kuo)executables}} | dmenu $opts)"
  [[ -z "$choice" ]] && exit 0
  if (( $+executables[$choice] )); then
      exec ${(z)executables[$choice]} &!
  else
      exec ${SHELL} -c "${choice}" &!
  fi
}

function power() {
  local opts=($defaults -w 3448 -p "  " -dim 0.42)

  typeset -A actions=(
      'Lock'     'loginctl lock-session'
      'Suspend'  'systemctl suspend'
      'Reboot'   'systemctl reboot'
      'Poweroff' 'systemctl poweroff'
  )
  
  menu="Lock\nSuspend\nReboot\nPoweroff"
  ${(z)actions[$(echo $menu | dmenu $opts)]}
}

$1
